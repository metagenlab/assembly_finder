import pandas as pd
community_name=config['community_name']

rule generate_assembly_tables:
    conda: '../envs/Assembly_finder.yml'

    output: 'tables/not_filtered/{entry}.tsv'

    params: NCBI_key = config['NCBI_key'], NCBI_email = config['NCBI_email'],
          comp=config['complete_assemblies'],ref=config['reference_assemblies'],
          rep=config['representative_assemblies'],met=config['exclude_from_metagenomes'],
          gb=config['Genbank_assemblies'],rs=config['Refseq_assemblies'],
          update=config['update_ete3']

    resources: ncbi_requests=1

    log: 'logs/not_filtered/{entry}.log'

    benchmark: "benchmark/find-assemblies/{entry}.txt"

    script: 'assembly_table.py'


rule filter_assembly_tables:
    conda: '../envs/Assembly_finder.yml'

    input: 'tables/not_filtered/{entry}.tsv'

    output: 'tables/filtered/{entry}.tsv'

    params: tb_path=config["input_table_path"], rank_filter=config['Rank_to_filter_by']

    log: 'logs/filtered/{entry}.log'

    script: 'filter_table.py'


entries=list(pd.read_csv(config['input_table_path'],delimiter='\t')['UserInputNames'])
rule combine_assembly_tables:
    conda: '../envs/Assembly_finder.yml'

    input: expand('tables/filtered/{entry}.tsv',entry=entries)

    output: f'{community_name}-assemblies-summary.tsv'

    script: 'combine_tables.py'

rule get_ftp_links_list:
    conda: '../envs/Assembly_finder.yml'

    input: f'{community_name}-assemblies-summary.tsv'

    output: temp(f"assembly_gz/{community_name}/{community_name}-ftp-links.txt")

    script: "concat-ftp-links.py"


checkpoint download_assemblies:
    conda: "../envs/download.yml"

    input: f"assembly_gz/{community_name}/{community_name}-ftp-links.txt"

    output: f"assembly_gz/{community_name}/{community_name}.done"

    log: f"logs/downloads/{community_name}.log"

    benchmark: f"benchmark/downloads/{community_name}.txt"

    params: f"assembly_gz/{community_name}"

    shell:
        """
        ascp -T -k 1 -i ${{CONDA_PREFIX}}/etc/asperaweb_id_dsa.openssh --mode=recv --user=anonftp \
        --host=ftp.ncbi.nlm.nih.gov --file-list={input} {params} &>> {log}
        touch {output}
        """