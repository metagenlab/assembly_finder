
rule generate_assembly_tables:
    conda: '../envs/Assembly_finder.yml'

    output: 'tables/not_filtered/{entry}.tsv'

    params: NCBI_key = config['NCBI_key'], NCBI_email = config['NCBI_email'],
          comp=config['complete_assemblies'],ref=config['reference_assemblies'],
          rep=config['representative_assemblies'],met=config['exclude_from_metagenomes'],
          gb=config['Genbank_assemblies'],rs=config['Refseq_assemblies']

    resources: ncbi_requests=1

    log: 'logs/not_filtered/{entry}.log'

    script: 'assembly_table.py'


rule filter_assembly_tables:
    conda: '../envs/Assembly_finder.yml'

    input: 'tables/not_filtered/{entry}.tsv'

    output: 'tables/filtered/{entry}.tsv'

    params: tb_path=config["input_table_path"], rank_filter=config['Rank_to_filter_by']

    log: 'logs/filtered/{entry}.log'

    script: 'filter_table.py'

import pandas as pd

def parse_table(input):
    table=pd.read_csv(input,delimiter='\t')
    names= list(table['UserInputNames'])
    expd=expand('tables/filtered/{entry}.tsv',entry=names)
    return expd

checkpoint combine_assembly_tables:
    conda: '../envs/Assembly_finder.yml'

    input: parse_table(config["input_table_path"])

    output: 'table_combined.tsv'

    script: 'combine_tables.py'

rule dl_assemblies:
    conda: '../envs/Assembly_finder.yml'

    input: 'table_combined.tsv'

    output: 'assembly_gz/{sample}.fna.gz'

    params: NCBI_email = config['NCBI_email']

    log: 'logs/assembly_gz/{sample}.log'

    script: 'dl.py'