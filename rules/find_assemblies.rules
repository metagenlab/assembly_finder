
rule generate_assembly_tables:
    conda: '../envs/Assembly_finder.yml'
    
    container: "docker://metagenlab/assemblyfinder:v.1.0"

    output: 'tables/not_filtered/{entry}.tsv'

    params: NCBI_key = config['NCBI_key'], NCBI_email = config['NCBI_email'],
          comp=config['complete_assemblies'],ref=config['reference_assemblies'],
          rep=config['representative_assemblies'],met=config['exclude_from_metagenomes'],
          gb=config['Genbank_assemblies'],rs=config['Refseq_assemblies']

    resources: ncbi_requests=1

    log: 'logs/not_filtered/{entry}.log'

    script: 'assembly_table.py'


rule filter_assembly_tables:
    conda: '../envs/Assembly_finder.yml'

    container: "docker://metagenlab/assemblyfinder:v.1.0"

    input: 'tables/not_filtered/{entry}.tsv'

    output: 'tables/filtered/{entry}.tsv'

    params: tb_path=config["input_table_path"], rank_filter=config['Rank_to_filter_by']

    log: 'logs/filtered/{entry}.log'

    script: 'filter_table.py'

import pandas as pd
entries=list(pd.read_csv(config['input_table_path'],delimiter='\t')['UserInputNames'])
checkpoint combine_assembly_tables:
    conda: '../envs/Assembly_finder.yml'

    container: "docker://metagenlab/assemblyfinder:v.1.0"

    input: expand('tables/filtered/{entry}.tsv',entry=entries)

    output: 'table_combined.tsv'

    script: 'combine_tables.py'

rule download_assemblies:
    conda: '../envs/Assembly_finder.yml'

    container: "docker://metagenlab/assemblyfinder:v.1.0"

    input: 'table_combined.tsv'

    output: 'assembly_gz/{sample}.fna.gz'

    params: NCBI_email = config['NCBI_email']

    log: 'logs/downloads/{sample}.log'

    script: 'dl.py'